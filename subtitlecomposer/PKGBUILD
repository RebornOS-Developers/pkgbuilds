# Maintainer: Rafael <rcostarega@gmail.com> from azul repository

pkgname=subtitlecomposer
pkgver=0.7.1
pkgrel=4
pkgdesc='An open source text-based subtitle editor that supports basic and advanced editing operations.'
arch=('x86_64')
url="https://invent.kde.org/multimedia/subtitlecomposer"
license=('GPL')
depends=('kcoreaddons' 'ktextwidgets' 'kio' 'sonnet' 'kcodecs' 'kross' 'kxmlgui' 'ki18n' 'ffmpeg' 'openal' 'pocketsphinx')
makedepends=('git' 'curl' 'cmake' 'extra-cmake-modules' 'autoconf' 'automake' 'libtool' 'xorg-server-xvfb' 'blas' 'ruby')

source=("git+${url}")
sha512sums=('SKIP')

build() {
  cmake -S "${srcdir}/${pkgname}" -B "${srcdir}/build" \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  cmake --build "${srcdir}/build"
}

check() {
  export DISPLAY=:99
  Xvfb :99 >& /dev/null &
  trap "kill $! || true" EXIT
  cmake --build "${srcdir}/build" --target test
}

package() {
  DESTDIR="${pkgdir}" cmake --install "${srcdir}/build"
}
